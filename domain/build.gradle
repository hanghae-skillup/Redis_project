plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.5'
	id 'io.spring.dependency-management' version '1.1.4'
}

dependencies {
	implementation project(':common')

	// Jakarta Persistence API (Spring Boot 3.x)
	implementation 'jakarta.persistence:jakarta.persistence-api:3.1.0'
	implementation "com.querydsl:querydsl-jpa:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
	// Annotation Processor (Jakarta 호환)
	annotationProcessor 'jakarta.persistence:jakarta.persistence-api:3.1.0'
	annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
}

tasks.named('bootJar') {
	enabled = false // bootJar 태스크 비활성화
}

tasks.named('jar') {
	enabled = true // 일반 JAR 파일 활성화
}

// QueryDSL 생성 경로 설정
def generatedSourcesDir = file("src/main/generated")

// clean 작업 시 QueryDSL 생성된 소스 삭제
tasks.named('clean') {
	delete generatedSourcesDir
}

// compileJava 작업 설정
tasks.named('compileJava') {
	options.annotationProcessorPath = configurations.annotationProcessor
	options.compilerArgs += ["-Ajpa=true"]
	options.generatedSourceOutputDirectory = generatedSourcesDir
}

// sourceSets에 QueryDSL 경로 추가
sourceSets {
	main.java.srcDir generatedSourcesDir
}